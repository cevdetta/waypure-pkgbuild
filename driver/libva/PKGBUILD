# Maintainer: cevdetta
# Contributor: cevdetta

pkgname=libva-waypure
pkgver=2.22.0
pkgrel=1
pkgdesc="Video Acceleration (VA) API for Linux (Wayland Only)"
arch=('x86_64')
url="https://github.com/intel/libva"
license=('MIT')
depends=('glibc' 'libdrm' 'wayland')
makedepends=('meson' 'wayland-protocols')
optdepends=(
  'intel-media-driver: backend for Intel GPUs (>= Broadwell)'
  'libva-intel-driver: backend for Intel GPUs (<= Haswell)'
  'libva-mesa-driver: backend for AMD and NVIDIA GPUs'
)
provides=('libva' 'libva.so' 'libva-drm.so' 'libva-wayland.so')
conflicts=('libva')
replaces=('libva')
source=("$url/releases/download/$pkgver/${pkgname%%-*}-$pkgver.tar.bz2")
sha256sums=('e3da2250654c8d52b3f59f8cb3f3d8e7fb1a2ee64378dbc400fbc5663de7edb8')

build() {
  local _meson_options=(
    --buildtype=release
    --auto-features=disabled
    -Dstrip=true
    -Db_lto=true
    -Db_ndebug=true

    # -Ddriverdir=
    -Ddisable_drm=false
    -Dwith_x11=no
    -Dwith_glx=no
    -Dwith_wayland=yes
    -Dwith_win32=no
    -Denable_docs=false
  )

  arch-meson "${pkgname%%-*}-$pkgver" build "${_meson_options[@]}"

  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 "${pkgname%%-*}-$pkgver/COPYING" -t "$pkgdir/usr/share/licenses/${pkgname%%-*}"
}
