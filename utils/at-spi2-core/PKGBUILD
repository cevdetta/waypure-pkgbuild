# Maintainer: cevdetta
# Contributor: cevdetta

pkgname=at-spi2-core-waypure
pkgver=2.58.1
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (Wayland only)"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/at-spi2-core"
license=('LGPL-2.1-or-later')
depends=('dbus' 'gcc-libs' 'glib2' 'systemd-libs')
makedepends=('glib2-devel' 'meson' 'systemd')
provides=('at-spi2-core' 'libatk-1.0.so' 'libatk-bridge-2.0.so' 'libatspi.so')
conflicts=('at-spi2-core')
replaces=('at-spi2-core')
source=("$url/-/archive/$pkgver/at-spi2-core-$pkgver.tar.gz")
sha256sums=('780544354db3f6b3e4cc0c6dcdf3bfbaac93970a7a1e7e659f24c7308e2b0a1a')

build() {
  local _meson_options=(
    --buildtype=release
    --auto-features=disabled
    -Dstrip=true
    -Db_lto=true
    -Db_ndebug=true

    -Ddefault_bus=dbus-broker
    -Duse_systemd=true
    -Dgtk2_atk_adaptor=false
    -Ddocs=false
    -Dintrospection=disabled
    -Dx11=disabled
    -Datk_only=false
  )

  arch-meson "at-spi2-core-$pkgver" build "${_meson_options[@]}"

  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
