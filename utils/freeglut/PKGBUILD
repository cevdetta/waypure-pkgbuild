# Maintainer: cevdetta
# Contributor: cevdetta

pkgname=freeglut-waypure
pkgver=3.8.0
pkgrel=1
pkgdesc="Free OpenGL Utility Toolkit"
arch=('x86_64')
url="https://github.com/FreeGLUTProject/freeglut"
license=('X11')
depends=('glibc' 'libglvnd' 'libxkbcommon' 'wayland')
makedepends=('cmake' 'glu' 'mesa' 'ninja')
provides=('freeglut' 'glut')
conflicts=('freeglut' 'glut')
replaces=('freeglut' 'glut')
source=("$url/releases/download/v$pkgver/${pkgname%%-*}-$pkgver.tar.gz")
sha256sums=('674dcaff25010e09e450aec458b8870d9e98c46f99538db457ab659b321d9989')

build() {
  local _cmake_args=(
    -D CMAKE_BUILD_TYPE=Release
    -B build -G Ninja
  	-D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_LIBDIR=lib

    -D FREEGLUT_BUILD_DEMOS=OFF
    -D FREEGLUT_BUILD_SHARED_LIBS=ON
    -D FREEGLUT_BUILD_STATIC_LIBS=OFF
    -D FREEGLUT_GLES=OFF
    -D FREEGLUT_WAYLAND=ON
  )

  cmake -S "${pkgname%%-*}-$pkgver" "${_cmake_args[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  ln -s glut.pc "$pkgdir/usr/lib/pkgconfig/freeglut.pc"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "${pkgname%%-*}-$pkgver/COPYING"
}
