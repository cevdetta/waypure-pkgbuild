# Maintainer: cevdetta
# Contributor: cevdetta

pkgname=swaybg-waypure
pkgver=1.2.1
pkgrel=1
pkgdesc="Wallpaper tool for Wayland compositors without gdk-pixbuf"
arch=('x86_64')
url="https://github.com/swaywm/swaybg"
license=('MIT')
depends=('cairo' 'wayland')
makedepends=('meson' 'scdoc' 'wayland-protocols')
provides=('swaybg')
conflicts=('swaybg')
replaces=('swaybg')
source=("$url/releases/download/v$pkgver/${pkgname%%-*}-$pkgver.tar.gz")
sha256sums=('6af1fdf0e57b1cc5345febed786b761fea0e170943a82639f94cfaed7df84f8f')

build() {
  local _meson_options=(
    --buildtype=release
    --auto-features=disabled
    -Dstrip=true
    -Db_lto=true
    -Db_ndebug=true

    -Dman-pages=enabled
  )

  arch-meson "${pkgname%%-*}-$pkgver" build "${_meson_options[@]}"

  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 "${pkgname%%-*}-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 "${pkgname%%-*}-$pkgver/README.md" -t "$pkgdir/usr/share/doc/$pkgname"
}
