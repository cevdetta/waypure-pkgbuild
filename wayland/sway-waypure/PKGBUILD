# Maintainer: cevdetta
# Contributor: cevdetta

pkgname=sway-waypure
pkgver=1.11
pkgrel=1
pkgdesc="Tiling Wayland compositor without X and gdk-pixbuf"
arch=('x86_64')
url="https://github.com/swaywm/sway"
license=('MIT')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'json-c' 'libdrm' 'libevdev' 'libinput' 'libxkbcommon' 'pango' 'pcre2' 'pixman' 'systemd-libs' 'wayland' 'wlroots0.19')
makedepends=('meson' 'scdoc' 'wayland-protocols')
optdepends=(
  'brightnessctl: Brightness adjustment tool used in the default configuration'
  'foot: Terminal emulator used in the default configuration'
  'grim: Screenshot utility used in the default configuration'
  'i3status: Status line generation'
  'mako: Lightweight notification daemon'
  'polkit: System privilege control. Required if not using seatd service'
  'swaybg: Wallpaper tool for sway'
  'sway-contrib: Collection of user-contributed scripts for sway'
  'swayidle: Idle management daemon'
  'swaylock: Screen locker'
  'waybar: Highly customizable bar'
  'wmenu: Application launcher used in default config'
  'xdg-desktop-portal-gtk: Default xdg-desktop-portal for file picking'
  'xdg-desktop-portal-wlr: xdg-desktop-portal backend'
)
provides=('sway' 'wayland-compositor')
conflicts=('sway')
replaces=('sway')
backup=(
  etc/sway/config
  etc/sway/config.d/50-systemd-user.conf
)
install=sway.install
source=(
  "$url/releases/download/$pkgver/${pkgname%%-*}-$pkgver.tar.gz"
  "50-systemd-user.conf"
  "sway-portals.conf"
)
sha256sums=(
  '0e37a55b7c3379230e97e1ad982542b75016a0c7d6676198604e557f9b373dae'
  'c7871e84042d0e698e2274c8bb9fbfad8225ede90b33830de106442d09e2acc4'
  'd7c031d0c49c116f503d2ad45471741d2730bbbb6ac945f89c15650f1701ff57'
)

build() {
  local _meson_options=(
    --buildtype=release
    --auto-features=disabled
    -Dstrip=true
    -Db_lto=true
    -Db_ndebug=true

    -D werror=false

    -Ddefault-wallpaper=false
    -Dzsh-completions=false
    -Dbash-completions=true
    -Dfish-completions=false
    -Dswaybar=true
    -Dswaynag=true
    -Dtray=enabled
    -Dgdk-pixbuf=enabled
    -Dman-pages=enabled
    -Dsd-bus-provider=libsystemd
  )

  arch-meson "${pkgname%%-*}-$pkgver" build "${_meson_options[@]}"

  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 "${pkgname%%-*}-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 50-systemd-user.conf -t "$pkgdir/etc/sway/config.d/"
  install -Dm644 sway-portals.conf "$pkgdir/usr/share/xdg-desktop-portal/sway-portals.conf"
}
